sourceSets {

    modImpl {
        compileClasspath += main.compileClasspath
    }

}

modloader {
    mixin {
        generateOnly = true
        plugin = "nl.elec332.minecraft.loader.impl.fmod.ForgeMixinPlugin"
        compatibilityLevel = JavaVersion.VERSION_17
    }

    addMixinsToManifest = false
    importMetadata findProject(":neo")
    mainModSource = sourceSets.modImpl
}

dependencies {
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }
}

compileModImplJava {
    if (findProject(":neo") == null) {
        source("../neo/src/modImplShared/java")
    }
}

compileJava {
    getOptions().setRelease(16) // Temporary, core must be J8 later
}

[jar, devJar].each {j ->
    j.manifest {
        attributes(["FMLModType": "LIBRARY"])
    }
}

tasks.register("modImplJar", Jar) {
    archiveBaseName = "ElecLoaderMod"
    from sourceSets.modImpl.output
    manifest.attributes(["FMLModType": "MOD", "MixinConfigs": "elecloader.forge.mixins.json", "MixinConnector": "nl.elec332.minecraft.loader.impl.fmod.ForgeLikeMixinConnector"])
}
