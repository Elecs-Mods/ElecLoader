println "ProjectNF: " + name

sourceSets {

    modImpl {
        compileClasspath += main.compileClasspath
        java.srcDir("src/modImplShared/java")
    }

    modRun {
        java.setSrcDirs([])
        resources.setSrcDirs([])
        runtimeClasspath += rootProject.tasks.jar.outputs.files
    }

}

modloader {
    mixin {
        plugin = "nl.elec332.minecraft.loader.impl.fmod.ForgeLikeMixinPlugin"
    }
    runtimeSource = sourceSets.modRun
    importMetadata findProject(":forge")
}

generateModInfo.source sourceSets.modImpl
generateMixinJson.source sourceSets.modImpl

compileModRunJava.enabled = false
processModRunResources.enabled = false
mkdir(compileModRunJava.destinationDirectory)
mkdir(processModRunResources.destinationDir)

jar {
    manifest {
        attributes(["FMLModType": "LANGPROVIDER"])
    }
}

tasks.register("modImplJar", Jar) {
    archiveBaseName = "ElecLoaderMod"
    from sourceSets.modImpl.output
    manifest.attributes(["FMLModType": "MOD"])
}

configurations {
    modRunImplementation.extendsFrom implementation
}

tasks.modRunClasses.dependsOn(rootProject.tasks.jar)

dependencies {
    compileOnly(project(":repackaged"))
    compileOnly(project(":common"))
}
